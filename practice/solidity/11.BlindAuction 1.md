# BlindAuction 1

- [BlindAuction 1](#BlindAuction-1)
  - [概述](#%E6%A6%82%E8%BF%B0)
  - [目标](#%E7%9B%AE%E6%A0%87)
  - [前提要求](#%E5%89%8D%E6%8F%90%E8%A6%81%E6%B1%82)
  - [主要步骤](#%E4%B8%BB%E8%A6%81%E6%AD%A5%E9%AA%A4)
    - [合约使用](#%E5%90%88%E7%BA%A6%E4%BD%BF%E7%94%A8)
    - [合约操作](#%E5%90%88%E7%BA%A6%E6%93%8D%E4%BD%9C)
    - [合约结构](#%E5%90%88%E7%BA%A6%E7%BB%93%E6%9E%84)
    - [合约代码](#%E5%90%88%E7%BA%A6%E4%BB%A3%E7%A0%81)
  - [总结](#%E6%80%BB%E7%BB%93)
  - [练习](#%E7%BB%83%E4%B9%A0)
  - [参考](#%E5%8F%82%E8%80%83)

## 概述

从本篇文章起，我们就开始研究 solidity 官方的第三个示例合约，秘密竞价（盲拍）合约。

## 目标

- 了解 solidity 的语法特性
- 使用 Remix 部署和执行合约

## 前提要求

- 掌握 solidity 的基础语法
- 掌握 Remix 的基本使用


## 主要步骤

BlindAuction 是一个用于秘密竞价（盲拍）的示例代码，在竞价的过程中，其他人都没有办法知道出价人的实际出价。

在区块链这样分布式的数据库系统上，任何信息都能被其它的人看到，所以实现秘密竞价方式需要采用密码学的技巧来完成对出价的隐藏。

这个合约是对前系列提到的 SimpleAuction 合约为基础的，你先要熟悉那个合约。

### 合约使用

这里会涉及到两个不同的时间阶段，你认为有三个也是可以的，即：

- 竞价阶段：所有拍卖人在这个阶段都可以自由出价，但彼此实际出价不可见；
- 揭示阶段：所以拍卖人需要在这个阶段表明实际的出价；
- 拍卖结束：拍卖受益人收到最高出价。

选择好需要拍卖的东西、竞价的持续时间和揭示阶段的持续时间，然后部署这个合约，告诉需要参与的人合约地址，让大家来竞拍。竞拍时间结束后，进入提示阶段，在此阶段比价，最高出价者将获得胜利，并且竞拍所出价的以太坊可直接打到受益人的账户。

合约中的角色：

- 拍卖受益人：提取合约中的最高出价者所出的 Ether；
- 拍卖人：
   - 未赢得最高出价的拍卖人：可提取出所有你已经付出的 Ether；
   - 赢得最高出价的拍卖人：你的最高出价将被受益人提走。

### 合约操作

任何人都可以调用构造方法创建该合约，但需要填写好受益人的以太坊地址，当然，这个地址也可以是合约的创建者地址。

1. 竞价阶段
   
   拍卖人通过使用 bid() 方法竞价，竞价需要填上**实际竞价金额、是否为假出价和一个只有自己知道的密码生成的 Hash 值**，并填写支付的 Ether。这些组成可以有效的制造出假的竞价信息，用来迷惑其它竞价者。

   > 注意：这三个值很重要，如果搞错任何的一个，那么 Hash 值就会不一致，那你的这部分竞拍所出的 Ether 就没办法拿回来了。

2. 揭示阶段

   拍卖人通过使用 reveal() 方法提示出价，这个方法需要传入刚刚在竞价阶段用来生成 Hash 值的三个参数，即：实际竞价金额、是否为假出价和一个只有自己知道的密码。

   拍卖人使用 withdraw() 方法撤回未成功的出价。

   > 注意：一定要在提示阶段通过 reveal() 方法揭示出价，要不然，等到揭示期过了，你投入的竞价 Ether 就拿不回来了。

3. 拍卖结束
   
   任何人都可以调用 auctionEnd() 方法结束该拍卖，并将最高出价者所出的 Ether 打入到受益人的账户。

### 合约结构

- 结构体 Bid：记录竞拍信息，blindedBid 记录 Hash，deposit 记录传入的 Ether；
- 状态变量 beneficiary：address payable 类型，记录拍卖受益者，它可以发送 Ether （以太币）；
- 状态变量 biddingEnd：uint 类型，用于保存竞拍结束的时间戳；
- 状态变量 revealEnd：uint 类型，用于保存揭示期结束的时间戳；
- 状态变量 ended：bool 类型，用于记录拍卖是否完成，当为 true 的时候，拍卖结束；
- 状态变量 bids：mapping 类型，用来记录 address 到 Bid 数组的映射；
- 状态变量 highestBider：address 类型，记录最高出价者的以太坊地址；
- 状态变量 ghighestBid： uint 类型，记录最高出价者所出的 Ether；
- 状态变量 pendingReturns：mapping 类型，记录可返还的 Ether，当你在揭示期公共的竞价被别人超过时，你就可以提取出的已经出过的 Ether；
- 事件 AuctionEnded：发送拍卖已经结束的 log 信息，需要 emit 触发；
- 装饰器 onlyBefore：判断时间在传入的时间戳之前；
- 装饰器 onlyAfter：判断时间在传入的时间戳之后；
- 构造方法 constructor：通过传入拍卖持续时间、提示期持续时间、受益人地址创建；
- 公共方法 bid：传入实际竞价金额、是否为假出价和一个只有自己知道的密码的 hash 去竞价；
- 公共方法 reveal：揭示期使用，传入实际竞价金额、是否为假出价和一个只有自己知道的密码的真实值创建，如果进行了多次竞价，那么这些值都需要传入；
- 私有方法 placeBid：揭示期被 reveal() 调用，判断调用人的地址和真实出价是否为最高出价；
- 公共方法 withdraw：撤回已经被其它出价人超过的出价 Ether；
- 公共方法 auctionEnd：结束拍卖。


### 合约代码

基于 v0.5.9 的示例代码：

```
pragma solidity >0.4.23 <0.7.0;

contract BlindAuction {
    struct Bid {
        bytes32 blindedBid;
        uint deposit;
    }

    address payable public beneficiary;
    uint public biddingEnd;
    uint public revealEnd;
    bool public ended;

    mapping(address => Bid[]) public bids;

    address public highestBidder;
    uint public highestBid;

    // Allowed withdrawals of previous bids
    mapping(address => uint) pendingReturns;

    event AuctionEnded(address winner, uint highestBid);

    /// Modifiers are a convenient way to validate inputs to
    /// functions. `onlyBefore` is applied to `bid` below:
    /// The new function body is the modifier's body where
    /// `_` is replaced by the old function body.
    modifier onlyBefore(uint _time) { require(now < _time); _; }
    modifier onlyAfter(uint _time) { require(now > _time); _; }

    constructor(
        uint _biddingTime,
        uint _revealTime,
        address payable _beneficiary
    ) public {
        beneficiary = _beneficiary;
        biddingEnd = now + _biddingTime;
        revealEnd = biddingEnd + _revealTime;
    }

    /// Place a blinded bid with `_blindedBid` =
    /// keccak256(abi.encodePacked(value, fake, secret)).
    /// The sent ether is only refunded if the bid is correctly
    /// revealed in the revealing phase. The bid is valid if the
    /// ether sent together with the bid is at least "value" and
    /// "fake" is not true. Setting "fake" to true and sending
    /// not the exact amount are ways to hide the real bid but
    /// still make the required deposit. The same address can
    /// place multiple bids.
    function bid(bytes32 _blindedBid)
        public
        payable
        onlyBefore(biddingEnd)
    {
        bids[msg.sender].push(Bid({
            blindedBid: _blindedBid,
            deposit: msg.value
        }));
    }

    /// Reveal your blinded bids. You will get a refund for all
    /// correctly blinded invalid bids and for all bids except for
    /// the totally highest.
    function reveal(
        uint[] memory _values,
        bool[] memory _fake,
        bytes32[] memory _secret
    )
        public
        onlyAfter(biddingEnd)
        onlyBefore(revealEnd)
    {
        uint length = bids[msg.sender].length;
        require(_values.length == length);
        require(_fake.length == length);
        require(_secret.length == length);

        uint refund;
        for (uint i = 0; i < length; i++) {
            Bid storage bidToCheck = bids[msg.sender][i];
            (uint value, bool fake, bytes32 secret) =
                    (_values[i], _fake[i], _secret[i]);
            if (bidToCheck.blindedBid != keccak256(abi.encodePacked(value, fake, secret))) {
                // Bid was not actually revealed.
                // Do not refund deposit.
                continue;
            }
            refund += bidToCheck.deposit;
            if (!fake && bidToCheck.deposit >= value) {
                if (placeBid(msg.sender, value))
                    refund -= value;
            }
            // Make it impossible for the sender to re-claim
            // the same deposit.
            bidToCheck.blindedBid = bytes32(0);
        }
        msg.sender.transfer(refund);
    }

    // This is an "internal" function which means that it
    // can only be called from the contract itself (or from
    // derived contracts).
    function placeBid(address bidder, uint value) internal
            returns (bool success)
    {
        if (value <= highestBid) {
            return false;
        }
        if (highestBidder != address(0)) {
            // Refund the previously highest bidder.
            pendingReturns[highestBidder] += highestBid;
        }
        highestBid = value;
        highestBidder = bidder;
        return true;
    }

    /// Withdraw a bid that was overbid.
    function withdraw() public {
        uint amount = pendingReturns[msg.sender];
        if (amount > 0) {
            // It is important to set this to zero because the recipient
            // can call this function again as part of the receiving call
            // before `transfer` returns (see the remark above about
            // conditions -> effects -> interaction).
            pendingReturns[msg.sender] = 0;

            msg.sender.transfer(amount);
        }
    }

    /// End the auction and send the highest bid
    /// to the beneficiary.
    function auctionEnd()
        public
        onlyAfter(revealEnd)
    {
        require(!ended);
        emit AuctionEnded(highestBidder, highestBid);
        ended = true;
        beneficiary.transfer(highestBid);
    }
}
```

## 总结

秘密竞价（盲拍）合约展示了如果在所有信息可见的以太坊上隐藏重要信息的方法，它利用了密码学中的 hash 算法使真实出价不可见，因为它预留了一个揭示期用来为出价人揭示真实出价，通过这个揭示期公布各自出价人的出价信息。

但这里也需要注意两点，不然后果很严重，你的出价过的 Ether 就拿不回来了：
- 牢记所有实际竞价金额、是否为假出价和一个只有自己知道的密码；
- 在揭示期使用 reveal() 方法揭示出价信息；

## 练习

试着将合约部署到 Remix，并运行看看合约执行的效果。

## 参考

- [Solidity by Example](https://solidity.readthedocs.io/en/v0.5.9/solidity-by-example.html)


# Ballot 3

- [Ballot 3](#Ballot-3)
  - [概述](#%E6%A6%82%E8%BF%B0)
  - [目标](#%E7%9B%AE%E6%A0%87)
  - [前提要求](#%E5%89%8D%E6%8F%90%E8%A6%81%E6%B1%82)
  - [主要步骤](#%E4%B8%BB%E8%A6%81%E6%AD%A5%E9%AA%A4)
    - [giveRightToVote 方法](#giveRightToVote-%E6%96%B9%E6%B3%95)
    - [delegate 方法](#delegate-%E6%96%B9%E6%B3%95)
    - [memory 和 storage](#memory-%E5%92%8C-storage)
  - [总结](#%E6%80%BB%E7%BB%93)
  - [练习](#%E7%BB%83%E4%B9%A0)
  - [参考](#%E5%8F%82%E8%80%83)

## 概述

本文继续讨论 Ballot 合约中剩下部分的内容。

## 目标

- 了解 solidity 的语法特性
- 使用 Remix 部署和执行合约

## 前提要求

- 掌握 solidity 的基础语法
- 掌握 Remix 的基本使用

## 主要步骤

### giveRightToVote 方法 

该方法的作用是合约的创建者给予某人投票的权利，它需要传入投票人的以太坊地址。去掉所有的注释之后，giveRightToVote 方法的内容如下：
```
    function giveRightToVote(address voter) public {
        require(
            msg.sender == chairperson,
            "Only chairperson can give right to vote."
        );
        require(
            !voters[voter].voted,
            "The voter already voted."
        );
        require(voters[voter].weight == 0);
        voters[voter].weight = 1;
    }
```

1. 条件判断

   - 第一次判断：该方法的调用者是不是来自合约的创建人，不是则抛出错误；
   - 第二次判断：判断该投票人的 voted 值，需要没有投过票，即 voted 值为 false。注意，`!voters[voter].voted` 使用了 ! 这个表示取反操作，即 voters[voter].voted 需要为 false，取反后就会 true 了；
   - 第三次判断：判断该投票人的 weight 值是否为 0，为什么要进行这个判断呢？有两种情况：第一是我已经是刚新建的投票人了，weight 已经为 1，所以没必要再执行下面的语句了；第二是我是个投票人，还有好几个人委托我来投票，我还没有投过票，如果没这条语言的话，我的 weight 会被下面的语句重写为 1。是不是就 BUG 了。

2. 代码 `voters[voter].weight = 1;`

   该代码的功能实现了投票人的创建。

   在经过了上面的判断之后，使用前面我们在 constructor 中学到的结构体创建方法之一，`voters[voter].weight = 1;` 来创建这个投票人。

这个授予某人投票权利的方法是不是很简单，就是一行代码就实现了他的逻辑，其它的代码为判断是不是应该给予他投票权利。

### delegate 方法

该方法主要的功能是被投票的权利委托给其它的人，让他人帮你去投票，所以该方法需要传入委托人的以太坊地址。去掉注释后的代码如下：
```
    function delegate(address to) public {
        Voter storage sender = voters[msg.sender];
        require(!sender.voted, "You already voted.");

        require(to != msg.sender, "Self-delegation is disallowed.");

        while (voters[to].delegate != address(0)) {
            to = voters[to].delegate;

            require(to != msg.sender, "Found loop in delegation.");
        }

        sender.voted = true;
        sender.delegate = to;
        Voter storage delegate_ = voters[to];
        if (delegate_.voted) {
            proposals[delegate_.vote].voteCount += sender.weight;
        } else {
            delegate_.weight += sender.weight;
        }
    }
```
1. 方法声明 `function delegate(address to) public`

   公共方法 delegate 的主要功能是委托投票权利，所以它需要传入被委托人的以太坊地址，在这里就是 to 参数传入方法中。

2. 代码 `Voter storage sender = voters[msg.sender];`

   该方法通过 msg.sender 获得调用者的以太坊地址，并在 voters mapping 中查找到对于该地址的 Voter，即投票人，然后保存到 sender 变量中。
   
   由于使用了 Voter 结构体，同时在方法内部，所以这里需要指定是使用 storage 还是 memory。当然，这里指定使用 storage 关键字。它们的区别马上会提到。

3. 条件判断 `require(!sender.voted, "You already voted.");`

   判断 sender 是否投过票，通过查找 sender 这个结构体的 voted 属性，由于这里使用了 ! 取反操作符，所以需要它的值为 false 才能通过判断。它的意思就是 sender 还没有投过票。

4. 条件判断 `require(to != msg.sender, "Self-delegation is disallowed.");`

   这段代码主要作用是来判断你需要委托的人是不是自己，想想这样的话也没有什么实际上的意义：将投票权利委托给自己。

5. 代码 `while (voters[to].delegate != address(0))`

   while 循环，当它的条件为 true 的时候就执行循环，比方这里的 while 判断的内容为，判断被委托方的 delegate 属性是否为 0x00000...0000，也就被委托方：
   
   - 还没有委托给其它人的话，它的 delegate 就是 address(0)，条件不成立，跳过循环；
   - 已经将投票权利委托给了他人，它的 delegate 就不是 address(0)，条件成立，执行循环体：
   ```
   to = voters[to].delegate;
   require(to != msg.sender, "Found loop in delegation.");
   ```
   循环体的内容为取出被委托人的 delegate 地址，保存到 to 属性，并再次判断这个 to 属性是不是 msg.sender 方法调用者。如果不是的话，则再次执行 while 语句，这个 while 语句委托的委托人是不是有值，如果它也委托了其它人，则会再次执行下去，只到找到最终没有将投票权委托出去的人。

   它就是最终找到没有委托其它人投票的人作为更新后的 to 变量，也就是最终委托人的以太坊地址。

   > 注意：如果 while 循环过多的话，则可能会耗完执行方法的 gas，导致方法执行失败。

6. 代码 `sender.voted = true;`

   将发送者，也就是方法调用者的 voted 设置为 true，代表它已经投过票了。

7. 代码 `sender.delegate = to;`

   将发送者，也就是方法调用者的 delegate 属性设置为最终委托人的以太坊地址。

8. 代码 `Voter storage delegate_ = voters[to];`

   根据最终委托人，创建一个 delegate_ 的 Voter 类型变量，由于是结构体，所以需要指定关键词 memory 或 storage。

9. 代码
   
   最后是一段判断代码：
   ```
   if (delegate_.voted) {
            proposals[delegate_.vote].voteCount += sender.weight;
   } else {
            delegate_.weight += sender.weight;
   }
   ```
   当最终委托人的如果已经投过票的话，运行 `proposals[delegate_.vote].voteCount += sender.weight;`，直接将方法调用者的投票权重加到已经投过票的题案上，相当于跟投；
   
   否则，将方法调用者的投票权重加到最终委托人的权重（weight）属性上。

> 注意：这里有 delegate 方法是不是有个 BUG，即你可以将投票权委托给其它任何一个以太坊地址的投票人。这就看你的业务设计上允不允许这样做了。

### memory 和 storage

这两个关键字在上一篇文章中已经介绍过了，它们主要用在方法内的数组、结构体和 mapping 类型的前面。那他们有什么区别呢？

其实记住他们区别非常容易，就比如说上面的 delegate 方法，代码 `Voter storage sender = voters[msg.sender];`，再执行到后面的代码：`sender.voted = true;` 和 `sender.delegate = to;`，执行完成后，sender 的值将被保存在以太坊区块链上。

而如果使用的是 **memory** 这个关键字的话，如 `Voter memory sender = voters[msg.sender];`，则后面的代码：`sender.voted = true;` 和 `sender.delegate = to;`，执行完成后，并不会修改 sender 在链上的数据，这样的话这个 sender 调用该方法就会一直没投过票、没有委托到其它的地址。这可以严重的 BUG。

所以，在这里，你需要特别注意这两个关键字的使用。也就是：

- storage: 修改链上的数据；
- memory: 不会修改链上的数据，一般作为临时变量使用。

## 总结

本篇文章主要描述了 solidity 中的一些语言上的特性。

- 关键字：memory 和 storage
- 关键字：while

## 练习

- 观察 memory 和 storage 的不同

## 参考

- [Solidity by Example](https://solidity.readthedocs.io/en/v0.5.9/solidity-by-example.html)


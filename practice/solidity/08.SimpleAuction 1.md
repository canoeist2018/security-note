# SimpleAuction 1

- [SimpleAuction 1](#SimpleAuction-1)
  - [概述](#%E6%A6%82%E8%BF%B0)
  - [目标](#%E7%9B%AE%E6%A0%87)
  - [前提要求](#%E5%89%8D%E6%8F%90%E8%A6%81%E6%B1%82)
  - [主要步骤](#%E4%B8%BB%E8%A6%81%E6%AD%A5%E9%AA%A4)
    - [合约使用](#%E5%90%88%E7%BA%A6%E4%BD%BF%E7%94%A8)
    - [合约操作](#%E5%90%88%E7%BA%A6%E6%93%8D%E4%BD%9C)
    - [合约结构](#%E5%90%88%E7%BA%A6%E7%BB%93%E6%9E%84)
  - [总结](#%E6%80%BB%E7%BB%93)
  - [练习](#%E7%BB%83%E4%B9%A0)
  - [参考](#%E5%8F%82%E8%80%83)

## 概述

从本往篇文章起，我们就开始研究 solidity 官方的第二个示例合约，拍卖合约。

## 目标

- 了解 solidity 的语法特性
- 使用 Remix 部署和执行合约

## 前提要求

- 掌握 solidity 的基础语法
- 掌握 Remix 的基本使用

## 主要步骤

SimpleAuction 合约是一个简单的拍卖系统合约，它能完成最基本的拍卖功能。合约的复杂度和 Ballot 合约的复杂度相当，只是多了操作 Ether 的功能。

### 合约使用

选择好需要拍卖的东西和需要竞拍的持续时间，然后部署这个合约，告诉需要参与的人合约地址，让大家来竞拍。到达竞拍时间后，最高出价者获得胜利，并且竞拍所出价的以太坊可直接打到受益人的账户。

现在的智能合约并不能和实物物品关联，你也只能相信拍卖人受益人会给你你所赢得的拍卖物，比如我拍卖一瓶 82 年的雪碧，你赢得了该拍卖，也只能相信我给你发货。虽然区块链上会记录拍卖的部分信息，但还是需要超越区块链去信任现实社会中的人。

区块链技术现阶段并不能完美解决现实社会中的所有问题。

合约中的角色：

- 拍卖受益人：提取合约中的最高出价者所出的 Ether；
- 拍卖人：
   - 未赢得最高出价的拍卖人：可提取出所有你已经付出的 Ether；
   - 赢得最高出价的拍卖人：你的最高出价将被受益人提走。

### 合约操作

任何人都可以调用构造方法创建该合约，但需要填写好受益人的以太坊地址，当然，这个地址也可以是合约的创建者地址。

拍卖人通过使用 bid() 方法竞价，竞价需要填写支付的 Ether。

拍卖人的出价如果被其它人超过，则可以通过使用 withdraw 方法撤回已经出价所支付的所有 Ether。

只要拍卖时间到期，任何人都可以结束该拍卖，并将最高出价者所出的 Ether 打入到受益人的账户。

> 用的最多的以太币单位为：1 wei 和 1 ether(10^18 wei)，即一个以太币为 1 后面跟 18 个零的 wei 单位。

### 合约结构

- 状态变量 beneficiary：address payable 类型，记录拍卖受益者，它可以发送 Ether （以太币）；
- 状态变量 auctionEndTime：uint 类型，也就是 uint256，它用于记录时间戳，即为 1970-01-01 到现在的秒数；
- 状态变量 highestBider：address 类型，记录最高出价者的以太坊地址；
- 状态变量 ghighestBid： uint 类型，记录最高出价者所出的 Ether；
- 状态变量 pendingReturns：mapping 类型，记录可返还的 Ether，当你的竞价被别人超过时，你就可以提取出的已经出过的 Ether；
- 状态变量 ended：bool 类型，用于记录拍卖是否完成，当为 true 的时候，拍卖结束；
- 事件 HighestBidIncreased：发送最高出拍改变的 log 信息，需要 emit 触发；
- 事件 AuctionEnded：发送拍卖已经结束的 log 信息，需要 emit 触发；
- 构造方法 constructor：通过传入拍卖持续时间（秒数）和受益人地址创建合约；
- 方法 bid：出价，需要传入一定数量的 Ether；
- 方法 withdraw：撤回已出价的、但被其它人超过你出价的 Ether；
- 方法 auctionEnd：将拍卖结束，并提取最高出价的 Ether 到受益人。

```
pragma solidity >=0.4.22 <0.7.0;

contract SimpleAuction {
    // Parameters of the auction. Times are either
    // absolute unix timestamps (seconds since 1970-01-01)
    // or time periods in seconds.
    address payable public beneficiary;
    uint public auctionEndTime;

    // Current state of the auction.
    address public highestBidder;
    uint public highestBid;

    // Allowed withdrawals of previous bids
    mapping(address => uint) pendingReturns;

    // Set to true at the end, disallows any change.
    // By default initialized to `false`.
    bool ended;

    // Events that will be emitted on changes.
    event HighestBidIncreased(address bidder, uint amount);
    event AuctionEnded(address winner, uint amount);

    // The following is a so-called natspec comment,
    // recognizable by the three slashes.
    // It will be shown when the user is asked to
    // confirm a transaction.

    /// Create a simple auction with `_biddingTime`
    /// seconds bidding time on behalf of the
    /// beneficiary address `_beneficiary`.
    constructor(
        uint _biddingTime,
        address payable _beneficiary
    ) public {
        beneficiary = _beneficiary;
        auctionEndTime = now + _biddingTime;
    }

    /// Bid on the auction with the value sent
    /// together with this transaction.
    /// The value will only be refunded if the
    /// auction is not won.
    function bid() public payable {
        // No arguments are necessary, all
        // information is already part of
        // the transaction. The keyword payable
        // is required for the function to
        // be able to receive Ether.

        // Revert the call if the bidding
        // period is over.
        require(
            now <= auctionEndTime,
            "Auction already ended."
        );

        // If the bid is not higher, send the
        // money back.
        require(
            msg.value > highestBid,
            "There already is a higher bid."
        );

        if (highestBid != 0) {
            // Sending back the money by simply using
            // highestBidder.send(highestBid) is a security risk
            // because it could execute an untrusted contract.
            // It is always safer to let the recipients
            // withdraw their money themselves.
            pendingReturns[highestBidder] += highestBid;
        }
        highestBidder = msg.sender;
        highestBid = msg.value;
        emit HighestBidIncreased(msg.sender, msg.value);
    }

    /// Withdraw a bid that was overbid.
    function withdraw() public returns (bool) {
        uint amount = pendingReturns[msg.sender];
        if (amount > 0) {
            // It is important to set this to zero because the recipient
            // can call this function again as part of the receiving call
            // before `send` returns.
            pendingReturns[msg.sender] = 0;

            if (!msg.sender.send(amount)) {
                // No need to call throw here, just reset the amount owing
                pendingReturns[msg.sender] = amount;
                return false;
            }
        }
        return true;
    }

    /// End the auction and send the highest bid
    /// to the beneficiary.
    function auctionEnd() public {
        // It is a good guideline to structure functions that interact
        // with other contracts (i.e. they call functions or send Ether)
        // into three phases:
        // 1. checking conditions
        // 2. performing actions (potentially changing conditions)
        // 3. interacting with other contracts
        // If these phases are mixed up, the other contract could call
        // back into the current contract and modify the state or cause
        // effects (ether payout) to be performed multiple times.
        // If functions called internally include interaction with external
        // contracts, they also have to be considered interaction with
        // external contracts.

        // 1. Conditions
        require(now >= auctionEndTime, "Auction not yet ended.");
        require(!ended, "auctionEnd has already been called.");

        // 2. Effects
        ended = true;
        emit AuctionEnded(highestBidder, highestBid);

        // 3. Interaction
        beneficiary.transfer(highestBid);
    }
}
```

## 总结

通过本篇文章，了解到了 SimpleAuction 合约大致的功能和结构，后面的文章会继续分析这个合约的内容。

## 练习

试着将合约部署到 Remix，并运行看看合约执行的效果。

## 参考

- [Solidity by Example](https://solidity.readthedocs.io/en/v0.5.9/solidity-by-example.html)


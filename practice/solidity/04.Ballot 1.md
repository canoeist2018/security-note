# Ballot 1

- [Ballot 1](#Ballot-1)
  - [概述](#%E6%A6%82%E8%BF%B0)
  - [目标](#%E7%9B%AE%E6%A0%87)
  - [前提要求](#%E5%89%8D%E6%8F%90%E8%A6%81%E6%B1%82)
  - [主要步骤](#%E4%B8%BB%E8%A6%81%E6%AD%A5%E9%AA%A4)
    - [合约的使用](#%E5%90%88%E7%BA%A6%E7%9A%84%E4%BD%BF%E7%94%A8)
    - [合约的源码](#%E5%90%88%E7%BA%A6%E7%9A%84%E6%BA%90%E7%A0%81)
    - [NatSpec 注释](#NatSpec-%E6%B3%A8%E9%87%8A)
    - [结构体](#%E7%BB%93%E6%9E%84%E4%BD%93)
    - [mapping 类型](#mapping-%E7%B1%BB%E5%9E%8B)
    - [数组](#%E6%95%B0%E7%BB%84)
  - [总结](#%E6%80%BB%E7%BB%93)
  - [练习](#%E7%BB%83%E4%B9%A0)
  - [参考](#%E5%8F%82%E8%80%83)

## 概述

通过上两篇文章的内容，我们掌握了 solidity 的一些基本的用法和 Remix 编辑工具的使用，那本篇就开始研究一个复杂一点的官方示例，Ballot 合约，它是一个投票合约，它的功能是实现投票人给指定的议题投票。

通过这段代码，我们可以为每个议题创建一个智能合约，然后为合约建立投票的议题，再指定可以来投票的人。

投票完成后，通过指定的接口获得结果。

## 目标

- 了解 solidity 的语法特性
- 使用 Remix 部署和执行合约

## 前提要求

- 掌握 solidity 的基础语法
- 掌握 Remix 的基本使用

## 主要步骤

### 合约的使用

Ballot 合约是用来投票的，你可以先这样想提案的内容，比如：

- 合约主题团建包含提案：打 CS、 吃大餐、徒步、近郊游；
- 合约主题本周清洁员，包含提案：Alice、Bob、Jack、Lisa；

合约中的角色有：

- 管理员（chairperson）：部署合约，同时创建提案 -> 指派谁可以来投票；
- 用户（voters）：
   1. 给对应的提案投票；
   2. 委派其它人投票；
   3. 查询获胜的提案。

也就是这么一点功能了，如果你熟悉其它编程语言的话，会发现它一点儿也不难。但我还是要在这里给你介绍每一个 solidity 的语言要点。

当然，介绍过的语言特性这里就不会再提及了，你可以去参考前面的两篇文章的内容。

### 合约的源码
下面是官方投票合约的源代码，相对于我们以前学习的智能合约，那是相当的大了。

- 数据结构 Voter： 结构体，代表了一个投票人；
- 数据结构 Proposal： 结构体，代表了一个提案，也就是所有可投票项中的一个选项；
- 变量 chairperson： address 类型，合约创建者，也是投票管理者；
- 变量 voters：mapping 类型，保存所有的投票人，使用投票人的地址做为键；
- 变量 proposals：Proposal 数组，也就是所有的投票提案数组；
- 构造方法 constructor：合约部署的时候执行，需要以数组的形式传入所有提案的名称；
- 方法 giveRightToVote：通过使用地址，授予投票权给对应的人，仅 chairperson 使用；
- 方法 delegate：将投票权限委托给其他人使用；
- 方法 vote：给指定的提案投票；
- 方法 winningProposal：返回获胜提案的号码，记住他是从 0 开始的；
- 方法 winnerName：返回获胜提案的名称。
```
pragma solidity >=0.4.22 <0.7.0;

/// @title Voting with delegation.
contract Ballot {
    // This declares a new complex type which will
    // be used for variables later.
    // It will represent a single voter.
    struct Voter {
        uint weight; // weight is accumulated by delegation
        bool voted;  // if true, that person already voted
        address delegate; // person delegated to
        uint vote;   // index of the voted proposal
    }

    // This is a type for a single proposal.
    struct Proposal {
        bytes32 name;   // short name (up to 32 bytes)
        uint voteCount; // number of accumulated votes
    }

    address public chairperson;

    // This declares a state variable that
    // stores a `Voter` struct for each possible address.
    mapping(address => Voter) public voters;

    // A dynamically-sized array of `Proposal` structs.
    Proposal[] public proposals;

    /// Create a new ballot to choose one of `proposalNames`.
    constructor(bytes32[] memory proposalNames) public {
        chairperson = msg.sender;
        voters[chairperson].weight = 1;

        // For each of the provided proposal names,
        // create a new proposal object and add it
        // to the end of the array.
        for (uint i = 0; i < proposalNames.length; i++) {
            // `Proposal({...})` creates a temporary
            // Proposal object and `proposals.push(...)`
            // appends it to the end of `proposals`.
            proposals.push(Proposal({
                name: proposalNames[i],
                voteCount: 0
            }));
        }
    }

    // Give `voter` the right to vote on this ballot.
    // May only be called by `chairperson`.
    function giveRightToVote(address voter) public {
        // If the first argument of `require` evaluates
        // to `false`, execution terminates and all
        // changes to the state and to Ether balances
        // are reverted.
        // This used to consume all gas in old EVM versions, but
        // not anymore.
        // It is often a good idea to use `require` to check if
        // functions are called correctly.
        // As a second argument, you can also provide an
        // explanation about what went wrong.
        require(
            msg.sender == chairperson,
            "Only chairperson can give right to vote."
        );
        require(
            !voters[voter].voted,
            "The voter already voted."
        );
        require(voters[voter].weight == 0);
        voters[voter].weight = 1;
    }

    /// Delegate your vote to the voter `to`.
    function delegate(address to) public {
        // assigns reference
        Voter storage sender = voters[msg.sender];
        require(!sender.voted, "You already voted.");

        require(to != msg.sender, "Self-delegation is disallowed.");

        // Forward the delegation as long as
        // `to` also delegated.
        // In general, such loops are very dangerous,
        // because if they run too long, they might
        // need more gas than is available in a block.
        // In this case, the delegation will not be executed,
        // but in other situations, such loops might
        // cause a contract to get "stuck" completely.
        while (voters[to].delegate != address(0)) {
            to = voters[to].delegate;

            // We found a loop in the delegation, not allowed.
            require(to != msg.sender, "Found loop in delegation.");
        }

        // Since `sender` is a reference, this
        // modifies `voters[msg.sender].voted`
        sender.voted = true;
        sender.delegate = to;
        Voter storage delegate_ = voters[to];
        if (delegate_.voted) {
            // If the delegate already voted,
            // directly add to the number of votes
            proposals[delegate_.vote].voteCount += sender.weight;
        } else {
            // If the delegate did not vote yet,
            // add to her weight.
            delegate_.weight += sender.weight;
        }
    }

    /// Give your vote (including votes delegated to you)
    /// to proposal `proposals[proposal].name`.
    function vote(uint proposal) public {
        Voter storage sender = voters[msg.sender];
        require(sender.weight != 0, "Has no right to vote");
        require(!sender.voted, "Already voted.");
        sender.voted = true;
        sender.vote = proposal;

        // If `proposal` is out of the range of the array,
        // this will throw automatically and revert all
        // changes.
        proposals[proposal].voteCount += sender.weight;
    }

    /// @dev Computes the winning proposal taking all
    /// previous votes into account.
    function winningProposal() public view
            returns (uint winningProposal_)
    {
        uint winningVoteCount = 0;
        for (uint p = 0; p < proposals.length; p++) {
            if (proposals[p].voteCount > winningVoteCount) {
                winningVoteCount = proposals[p].voteCount;
                winningProposal_ = p;
            }
        }
    }

    // Calls winningProposal() function to get the index
    // of the winner contained in the proposals array and then
    // returns the name of the winner
    function winnerName() public view
            returns (bytes32 winnerName_)
    {
        winnerName_ = proposals[winningProposal()].name;
    }
}
```

### NatSpec 注释
首先，这里多了一种注释类型，就是那个以 /// 开头的注释，它是一种称为 NatSpec 的注释，也就是 Ethereum Natural Language Specification Format (NatSpec) 的简称。

它是一种给合约的调用者阅读的注释，是给人看的，一般将其加在所有公共方法的前面，用来描述方法的说明。

多行内容可以使用 /** */ 来操作。如：

```
pragma solidity ^0.4.0;

/** @title 形状计算器。 */
contract tinyCalculator {
    /** @dev 求矩形表明面积与周长。
    * @param w 矩形宽度。
    * @param h 矩形高度。
    * @return s 求得表面积。
    * @return p 求得周长。
    */
    function rectangle(uint w, uint h) returns (uint s, uint p) {
        s = w * h;
        p = 2 * (w + h);
    }
}
```
这个和其它编程语言中的文档注释功能是差不多的。

### 结构体

solidity 中定义结构体以 struct 开头，它是一种将多个数据类型集合在一起的聚合数据类型。比如说这个合约中有两个结构体类型：

Voter 结构体：
```
    // Voter 结构体代表一个投票人
    struct Voter {
        uint weight; // 权重，初始为 1，每当被其它人委托（delegate）的时候，加 1
        bool voted;  // bool 值，为真时表示已经投过票
        address delegate; // 将投票权委托给了谁（地址）
        uint vote;   // 已投票提案的索引号，它是从 0 开始的
    }
```

Proposal 结构体：
```
    // Proposal 结构体代表了一个提案
    struct Proposal {
        bytes32 name;   // 提案的名称，最大 32 字节，相当于 32 个英文字母或 10 个汉字（utf-8 编码）
        uint voteCount; // 获得的投票数
    }
```

看了上面的两个结构体的示例，你大致也可以了解到结构体的作用了。

### mapping 类型

mapping 类型我们在上篇文章中已经讲解过了，它同其它语言中的 map 是一样的，通过 key 可以获取到对应的 value。

上篇 Coin 合约中，balances 的类型就是 mapping，它是通过 address 获取这个地址拥有的 token 的数量，也就是 `mapping (address => uint) public balances;`。

但这里有点不同，`mapping(address => Voter) public voters;` 也是通过地址，但它返回的是一个刚刚介绍的结构体类型 Voter。也就是返回值是一个聚合类型，它包括了 Voter 里面所有的四个值。一次性返回四个值，是不是很方便。

如果去查询一个在 voters 中不存在的地址，则会返回 Voter 结构体中数据类型的默认值。如 Voter 结构体默认值为：

- uint weight: 0
- bool voted: false
- address delegate: 0x0000000000000000000000000000000000000000
- uint vote: 0

### 数组

数组一般用来保存相同类型的集合，如果类型为 T，长度为 k，则它的声明方法是 T[k]。当然可以省略 k ，也就是声明方式变成了 T[] ，它是一个动态数组 。

在大多数编程语言中，当然包括 solidity，数组的索引号都是从 0 开始的，一直到长度 - 1 的索引。如：T[4]，就有四个元素类型，包含 T[0]，T[1]，T[2]，T[3] 这四个元素。 

访问超出的索引值会报错，如上面的 T[4]，你去访问 T[4] 就会报错。

这里的 `Proposal[] public proposals;` 就声明了一个 Proposal 的动态数组类型。

## 总结

由于这个 Ballot 合约的内容相对较多，所以剩下的内容放在了下一篇文章在做介绍。

在这里，我们了解到了 solidity 的一些语法特性：

- 注释：NatSpec 注释
- 类型：结构体、mapping、数组

## 练习

先将这个合约部署到 Remix 中，然后使用试试看。

## 参考

- [Solidity by Example](https://solidity.readthedocs.io/en/v0.5.9/solidity-by-example.html)

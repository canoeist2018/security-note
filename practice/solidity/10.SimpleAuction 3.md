# SimpleAuction 3

- [SimpleAuction 3](#SimpleAuction-3)
  - [概述](#%E6%A6%82%E8%BF%B0)
  - [目标](#%E7%9B%AE%E6%A0%87)
  - [前提要求](#%E5%89%8D%E6%8F%90%E8%A6%81%E6%B1%82)
  - [主要步骤](#%E4%B8%BB%E8%A6%81%E6%AD%A5%E9%AA%A4)
  - [主要步骤](#%E4%B8%BB%E8%A6%81%E6%AD%A5%E9%AA%A4-1)
    - [bid 方法](#bid-%E6%96%B9%E6%B3%95)
    - [withdraw 方法](#withdraw-%E6%96%B9%E6%B3%95)
    - [auctionEnd 方法](#auctionEnd-%E6%96%B9%E6%B3%95)
  - [总结](#%E6%80%BB%E7%BB%93)
  - [练习](#%E7%BB%83%E4%B9%A0)
  - [参考](#%E5%8F%82%E8%80%83)

## 概述

从本往篇文章起，我们就开始研究 solidity 官方的第二个示例合约，拍卖合约。

## 目标

- 了解 solidity 的语法特性
- 使用 Remix 部署和执行合约

## 前提要求

- 掌握 solidity 的基础语法
- 掌握 Remix 的基本使用
## 主要步骤

## 主要步骤

### bid 方法

该方法的主要功能是出价，调用它需要附上一定量的 Ether。我们想像中的出价的编写思路应该是这样的：

- 限制条件：
   1. 拍卖并没有结束；
   2. 我出的价必须大于已有的最高出价；

- 满足条件后执行：
  1. 如果是该拍卖的第一次出价，则直接保存我的信息到最高出价信息中；
  2. 如果有人出过价，则将他的信息保存到待返回列表，并将我的信息保存到最高出价信息中。

最高出价信息包括最高出价者的以太坊地址和出价的金额。

出价方法的代码如下：
```
    function bid() public payable {
        require(now <= auctionEndTime, "Auction already ended.");

        require(msg.value > highestBid, "There already is a higher bid.");

        if (highestBid != 0) {
            // 简单的使用 highestBidder.send(highestBid) 发回 Ether 有安全风险，
            // 因为它可能会执行非信任合约。
            // 更为安全的做法是让接受者自已提取 Ether 。
            pendingReturns[highestBidder] += highestBid;
        }
        highestBidder = msg.sender;
        highestBid = msg.value;
        emit HighestBidIncreased(msg.sender, msg.value);
    }
```

1. 方法声明 `function bid() public payable`

   bid() 方法在声明的时候使用了 payable 关键字，有了这个关键字之后，这个方法就可以授受 Ether 了，收到的 Ether 就会被保存到合约的地址上。
   
   payable 方法可以被当做是非 payable 方法使用，即发送 0 个 Ether，它就是非 payable 方法了。相反，非 payable 方法是不能当成 payable 方法使用的。

   下面是一个示例，使用了 payable 关键字的方法可以接受到 Ether，而另一个方法确不行。发送 Ether 后，可以通过合约地址去查询合约上的 Ether，如果在 Remix 中，需要通过上篇文章 BalanceOf 合约去查询。

   ```
   pragma solidity >=0.4.22 <0.7.0;
        contract test {
            function receiveEther() public payable {
            }
            
            function notReceiveEther() public pure {
            }
        }
   ```
   
   > 注意：第二个方法使用了 pure 关键词，是因为该方法即没有读取，也没有改变链上的状态。

   > 注意：打到这个合约地址上的 Ether 再也拿不回来了，因为没有使用取回 Ether 的方法。

2. 条件判断 `require(now <= auctionEndTime, "Auction already ended.");`

   auctionEndTime 这个变量是通过在合约创建时的时间戳加入这个拍卖需要执行的时间创建的。
   
   当然行到 bid() 方法时，执行时间 now 如果小于 auctionEndTime 表示还没有到拍卖的结束时间，则可以继续执行后面的步骤。
   
   执行 bid() 方法的时间如果大于 auctionEndTime，表示拍卖结束，则会返回错误 "Auction already ended."。

3. 条件判断 `require(msg.value > highestBid, "There already is a higher bid.");`

   如果传入的值大于当前最高的拍卖出价 highestBid，则执行后继的操作；
   
   如果小入当前最高的拍卖出价 highestBid，则停止执行后继步骤，返回错误提示："There already is a higher bid."。

4. 判断 `if (highestBid != 0)`

   当前的最高出价 highestBid 是否为 0，这里有两种可能：
   - 如果 highestBid 是 0：则合约还没有人出过价；
   - 如果 highestBid 不是 0：则有人出过价，且保管的是最高出价。

   在有人出过价的情况下，会执行 `pendingReturns[highestBidder] += highestBid;` 将最高出价人的地址和最高出价保存到 pendingReturns 这个等待提出 Ether 的 mapping 结构体中。
   
   > 注意：这里采用的方法是让出价者自己通过 withdraw() 方法来提出 Ether，而不是在这里直接返回 Ether，这是从安全方法考虑的。

5. 执行语句

   ```
   highestBidder = msg.sender;
   highestBid = msg.value;
   emit HighestBidIncreased(msg.sender, msg.value);
   ```

   将当前的出价者保存到最高出价者变量 highestBidder 中，将其出价的 Ether 保存到 最高出价金额 highestBid 中，并使用 emit 发出 log，它是通过我们在合约中开始定义的 Event 事件发送出去的。

   > 注意：上面的判断方法中，已经将最高出价者的地址和金额保存到了 pendingReturns 可提取行列中，所以这里可以覆盖掉它们的值。

### withdraw 方法

withdraw 方法用于返回自己已经发送但被其它人超过的所有金额。编写思路为：

- 通过我的以太坊地址去可返回的金额列表中查找，获取可返还的具体数量；
- 如果可返还金额大于 0 则返回。

```
    function withdraw() public returns (bool) {
        uint amount = pendingReturns[msg.sender];
        if (amount > 0) {
            // 先设置账户为 0 是为了防止在 send 返回前调用其它的方法，即造成回调漏洞
            pendingReturns[msg.sender] = 0;

            if (!msg.sender.send(amount)) {
                // 这里没有抛出异常，只是重置了 Ether
                pendingReturns[msg.sender] = amount;
                return false;
            }
        }
        return true;
    }
```

1. 方法声明 `function withdraw() public returns (bool)`

   直接调用该访问，无需要传入任何参数，如果收到 Ether 则返回 true，否则返回 false。

2. 代码 `uint amount = pendingReturns[msg.sender];`

   查找待返回 Ether 列表，并将找到的值保存到 amount 临时变量中。

3. 代码 `if (amount > 0)`

   判断 amount 是否大于 0，也就是调用 withdraw() 方法的用户是否有 Ether 在待返回 Ether 列表中有 Ether。

   注意，这里有写法同其它语言的实现有所不同。即如果有 Ether 的话，则先将待返回列表中调用者的可提取 Ether 置为 0。然后再调用 send() 方法转账。
   
   判断 send() 方法的返回结果：
      - 如果为 false 的话，转账失败，将重新将待返回列表的 Ether 数量设置为 amount 的数量，并使 withdraw() 方法返回 false。
   
      - 如果为 true 的话，转账成功，会执行 withdraw() 方法的最后一行返回 true。

   > 注意：这里的写法是先清空了需要操作 Ether 的账号中保存的金额，这是因为执行转账操作时，可能会被目标地址的回调函数恶意再次调用该方法，导致重入漏洞并清空完合约上的所有 Ether。

### auctionEnd 方法

auctionEnd 方法的功能是结束拍卖，然后将最高的拍卖收款支付给拍卖的获益人。编写代码的思路为：

- 限制条件：
   1. 拍卖超过了最大的拍卖时间；
   2. 拍卖并没有被执行过结束拍卖操作（不然还可以继续转出钱给获益人）；

- 满足条件后执行：
   1. 设置标识拍卖已经结束的状态值 ended 为 true；
   2. 转账给获益人；
   3. 发送日志信息。

```
    function auctionEnd() public {
        // 一个好的指导是和其它合约（调用方法或发送 Ether）时执行三步：
        // 1. 检查条件
        // 2. 执行动作
        // 3. 和其它合约交互

        // 1. 条件
        require(now >= auctionEndTime, "Auction not yet ended.");
        require(!ended, "auctionEnd has already been called.");

        // 2. 生效
        ended = true;
        emit AuctionEnded(highestBidder, highestBid);

        // 3. 交互
        beneficiary.transfer(highestBid);
    }
```

1. 方法声明 `function auctionEnd() public`

   方法 auctionEnd() 为公共方法，且不需要返回值。

2. 判断条件 `require(now >= auctionEndTime, "Auction not yet ended.");`

   现在的时间必须大于拍卖的最大持续时间，也就是拍卖已经可以结束了。

3. 判断条件 `require(!ended, "auctionEnd has already been called.");`

   状态变量 ended 为 false，因为如果该值为 true 的话，就表明已经执行过这个方法了，已经执行过这个方法表示已经给拍卖获益人转过最高拍卖金融所获得的 Ether 了。不然，只要执行这个方法就可以给获益人转 Ether，那其它的人就没有多余的 Ether 从 pendingReturns 中取回属于它的 Ether 了。

4. 代码 `ended = true;`

   设置 ended 为 true，表明拍卖已经结束。

5. 代码 `emit AuctionEnded(highestBidder, highestBid);`

   利用前面定义好的 `event AuctionEnded(address winner, uint amount);` 发送 log 信息。

6. 代码 `beneficiary.transfer(highestBid);`

   使用 transfer() 方法转账，同 send() 方法不同的是，transfer() 方法不回返回结束，如果转账失败的话，就会回滚执行的操作。

## 总结

通过本篇文章，了解到了 SimpleAuction 合约使用 Ether 的情况，因为一旦使用到了 Ether，你就需要加倍注意安全风险，如果方法使用不刚或者逻辑出错，轻则导致 Ether 丢失，重则使合约中的 Ether 被清空。

本示例中采用的两种合约的转账方式，都是需要重点掌握的。

- 关键字：payable；
- 方法：转账方法 send() 和 transfer()。

## 练习

- 试着将合约部署到 Remix，并运行看看合约执行的效果。
- 观察 payable 关键字在方法上使用的效果。

## 参考

- [Solidity by Example](https://solidity.readthedocs.io/en/v0.5.9/solidity-by-example.html)

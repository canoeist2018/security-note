# BlindAuction 2

- [BlindAuction 2](#BlindAuction-2)
  - [概述](#%E6%A6%82%E8%BF%B0)
  - [目标](#%E7%9B%AE%E6%A0%87)
  - [前提要求](#%E5%89%8D%E6%8F%90%E8%A6%81%E6%B1%82)
  - [主要步骤](#%E4%B8%BB%E8%A6%81%E6%AD%A5%E9%AA%A4)
    - [构造方法 constructor](#%E6%9E%84%E9%80%A0%E6%96%B9%E6%B3%95-constructor)
    - [bid 方法](#bid-%E6%96%B9%E6%B3%95)
    - [reveal 方法](#reveal-%E6%96%B9%E6%B3%95)
    - [方法 placeBid](#%E6%96%B9%E6%B3%95-placeBid)
    - [方法 withdraw](#%E6%96%B9%E6%B3%95-withdraw)
    - [方法 auctionEnd](#%E6%96%B9%E6%B3%95-auctionEnd)
  - [总结](#%E6%80%BB%E7%BB%93)
  - [练习](#%E7%BB%83%E4%B9%A0)
  - [参考](#%E5%8F%82%E8%80%83)

## 概述

本文将介绍 solidity 官方的第三个示例合约，秘密竞价（盲拍）合约中的各方法。

## 目标

- 了解 solidity 的语法特性
- 使用 Remix 部署和执行合约

## 前提要求

- 掌握 solidity 的基础语法
- 掌握 Remix 的基本使用
- 了解 SimpleAuction 合约

## 主要步骤

### 构造方法 constructor

这个构造方法大家都已经很熟悉了，它是部署合约时需要执行的方法，它需要传入三个值：
- _biddingTime 竞拍时间：uint 类型，表示竞拍活动的持续时间，以秒为单位，如 3600 表示竞拍持续 3600 秒；
- _revealTime 揭示期时间：uint 类型，表示揭示期活动的持续时间，同样以秒为单位；
- _beneficiary 受益人：address 类型，保存该拍卖活动的受益人以太坊地址，竞拍获胜者的 Ether 会打到该地址；

```
constructor(uint _biddingTime, uint _revealTime, address payable _beneficiary) public {
    beneficiary = _beneficiary;
    biddingEnd = now + _biddingTime;
    revealEnd = biddingEnd + _revealTime;
}
```
代码的内容是相当的简单，now 方法的返回当前区块的时间戳，在上一个 SimpleAuction 合约中讲过了。

### bid 方法

bid 方法需要传入一个参数，即由真实竞价、是否为假竞价、秘密组合后生成的 hash 值，但注意它是带有 payable 关键字的，所以还可以传入一定数量的 Ether。成功的竞价需要的条件之一就是传入的 Ether 要大小真实竞价的值的。

> 注意这里的真实竞价单位为 wei，也就是 1 Ether = 1 * 10^18 wei，比较也是以 wei 为单位的比较。

```
function bid(bytes32 _blindedBid) public payable onlyBefore(biddingEnd) {
    bids[msg.sender].push(Bid({blindedBid: _blindedBid, deposit: msg.value}));
}
```

该方法的内容也很简单，合约中是用一行语句来完成了，它实际上完成了两步操作：

- 从 bids 这个 mapping 中，`mapping(address => Bid[]) public bids;` 通过调用这个 bid() 方法的以太坊地址，查找该地址所有已竞价 hash 值和 Ether 金融组成的 Bid 数组。如果该地址以前没有竞价过，则会返回空数组；
- 然后，调用上面返回的数组的 push 方法，将本次的竞价信息加入到数组的末尾。

> 注意：忘了发送 Ether 也会让该方法调用成功，但本次竞价你不会赢得胜利。

上节课用到的 blindedBid 合约如下，用它来生成 bid() 方法使用到的 keccak256 hash 值：
```
pragma solidity >=0.4.22 <0.7.0;

contract BlindedBid {
    function blindedBid(uint value, bool fake, bytes32 secret) public pure returns(bytes32) {
        return keccak256(abi.encodePacked(value, fake, secret));
    }
}
```

### reveal 方法

该方法主要用于在揭示期揭示你的实际出价，需要把你已经在竞价阶段所有竞价都提交。所有的竞价信息都以数组的方式提交。

> 注意：如果你出价了 5 次，但你实际上只记下来了四次，有一次忘了记录，那对不起，你不仅赢不了竞价，同时所有付出的 Ether 都拿不回来了。

```
function reveal(uint[] memory _values, bool[] memory _fake, bytes32[] memory _secret) public onlyAfter(biddingEnd) onlyBefore(revealEnd) {
        uint length = bids[msg.sender].length;
        require(_values.length == length);
        require(_fake.length == length);
        require(_secret.length == length);

        uint refund;
        for (uint i = 0; i < length; i++) {
            Bid storage bidToCheck = bids[msg.sender][i];
            (uint value, bool fake, bytes32 secret) =
                    (_values[i], _fake[i], _secret[i]);
            if (bidToCheck.blindedBid != keccak256(abi.encodePacked(value, fake, secret))) {
                // Bid was not actually revealed.
                // Do not refund deposit.
                continue;
            }
            refund += bidToCheck.deposit;
            if (!fake && bidToCheck.deposit >= value) {
                if (placeBid(msg.sender, value))
                    refund -= value;
            }
            // Make it impossible for the sender to re-claim
            // the same deposit.
            bidToCheck.blindedBid = bytes32(0);
        }
        msg.sender.transfer(refund);
    }
```

这个方法稍微有点复杂，它比我们以前学过的方法都要长一点，逻辑多一点，而且它还调用了一个 placeBid() 的内部方法。

1. 方法声明 `function reveal(uint[] memory _values, bool[] memory _fake, bytes32[] memory _secret) public onlyAfter(biddingEnd) onlyBefore(revealEnd)`

   首先该方法需要传入你所有竞价过的真实竞价的列表、是否为假竞价的列表、密码列表，它们都是以数组的形式传入的。比如说以下面的方式传入两次竞价的信息：
   ```
   ["10000000000000000000","2000000000000000000"],[false,false],["0x3125730000000000000000000000000000000000000000000000000000000000","0x3125730000000000000000000000000000000000000000000000000000000000"]
   ```
   然后，方法使用了两个修饰器，它们都需要创建合约时生成的时间点参数：
   - onlyAfter(biddingEnd)：需要在竞价时间点结束后；
   - onlyBefore(revealEnd)：需要在揭示时间点到达前；
   
2. 代码 `uint length = bids[msg.sender].length;`

   获取了 bids 中保存的基于方法调用者以太地址的数组，并通过 length 获取了数组的长度。也就是获取了该地址所有在竞价阶段的出价次数。

3.  判断代码

   ```
   require(_values.length == length);
   require(_fake.length == length);
   require(_secret.length == length);
   ```
   这三行代码判断你传入的真实竞价数组、是否为假竞价数组、密码数组的长度是否等于你所参与竞价的次数。所以这就是为什么说你忘掉一次竞价信息都不会通过这几条判断代码。

4. 代码 `uint refund;`

   定义了需要返还的以太币的数量，以 wei 为单位。

5. 代码 `for (uint i = 0; i < length; i++)`

   遍历你的所有竞价，再根据情况执行后续操作。

   1. 代码 `Bid storage bidToCheck = bids[msg.sender][i];`
   
      先将第 i 个竞价信息取出来，里面保存的是竞价信息的 hash 值和打入的以太币；

   2. 代码 ` (uint value, bool fake, bytes32 secret) = (_values[i], _fake[i], _secret[i]);`
   
      将传入的真实竞价、是否为假竞价、密码数组的第 i 组分别存入到 value，fake，secret 三个变量中；

   3. 判断代码
      ```
      if (bidToCheck.blindedBid != keccak256(abi.encodePacked(value, fake, secret))) {
                 continue;
            }
       ```
       看你传入的 value，fake，secret 三个值生成的 hash 是否和竞价时一致，如果不一致则会执行下一个循环。在这里你会看到，如果你打了以太币则不会加入到 refund 中，也就无法返还了。

   4. 代码 `refund += bidToCheck.deposit;`

      将真实打入的 Ether 加入到 refund 中，待返还给方法调用者；

   5. 判断代码
      ```
      if (!fake && bidToCheck.deposit >= value) {
                if (placeBid(msg.sender, value))
                    refund -= value;
            }
      ```
    上面的判断条件不满足的话，你就可以拿回你本次总价的以太币了：
    - 是一个假的竞价，需要 !fake 为 false；
    - 你打入的以太币小于你的真实出价；
    
    不然，你就会进入 placeBid() 这个内部方法，传你的以太坊地址和真实出价，后面会讲。
    
    上面的内部方法判断成功后，就会执行 `refund -= value` ，相当于将真实的出价从你传入的 Ether 中减掉。也就是在 bid() 方法中用于迷惑对手的技巧，上面已经判断过 `bidToCheck.deposit >= value` 传入的 Ether 会大于你的真实竞价；
    
    6. 代码 `bidToCheck.blindedBid = bytes32(0);`

       将竞拍的 hash 值设置成为全 0 值，这样下次验证就不会通过第 3 步的判断逻辑了，就进入不了后面的代码以防合约上的以太币被转走。

6. 代码 `msg.sender.transfer(refund);`

   将需要返还的钱转给该方法的调用者。

### 方法 placeBid

这个方法和前面的 SimpleAuctin 合约中的 bid() 方法中的内容差不多，就是将你的以太坊地址和真实竞价保存到最高竞价者和最高价状态变量中：
```
    function placeBid(address bidder, uint value) internal returns (bool success)
    {
        if (value <= highestBid) {
            return false;
        }
        if (highestBidder != address(0)) {
            // Refund the previously highest bidder.
            pendingReturns[highestBidder] += highestBid;
        }
        highestBid = value;
        highestBidder = bidder;
        return true;
    }
```

### 方法 withdraw

假竞价的情况和真实竞价小于打入以太币的情况下，你打入的以太币都会通过 reveal() 方法返还给你们。而调用 withdraw 是用在揭示期时，竞价被人超过的情况下，将以太币加入 pendingReturns 状态变量后，返还给你的。和 SimpleAuctin 合约中的 withdraw() 差不多：

```
    function withdraw() public {
        uint amount = pendingReturns[msg.sender];
        if (amount > 0) {
            // It is important to set this to zero because the recipient
            // can call this function again as part of the receiving call
            // before `transfer` returns (see the remark above about
            // conditions -> effects -> interaction).
            pendingReturns[msg.sender] = 0;

            msg.sender.transfer(amount);
        }
    }
```

### 方法 auctionEnd

和 SimpleAuctin 合约中的 auctionEnd() 方法基本一样，这里就不解释了。

```
    function auctionEnd() public onlyAfter(revealEnd) {
        require(!ended);
        emit AuctionEnded(highestBidder, highestBid);
        ended = true;
        beneficiary.transfer(highestBid);
    }
```

## 总结

本文介绍了 BlindAuction 合约中用到的所有方法，其中比较难以理解的就是它的 reveal() 方法，因为这个方法中的逻辑是最多的。

其它的方法要么很简单，要么就和 SimpleAuctin 合约中的方法类似。

## 练习

将本 BlindedBid 合约部署到 Remix 中，并运行了解具体的操作。

## 参考

- [Solidity by Example](https://solidity.readthedocs.io/en/v0.5.9/solidity-by-example.html)

